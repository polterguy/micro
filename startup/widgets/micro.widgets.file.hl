/*
 * Creates the __[micro.widgets.file]__ extension widget.
 */

/*
 * This widget will automatically load all specified Hyperlambda files,
 * and append these as children to itself. You can supply widgets to it with
 * the following arguments.
 *
 * - __[folder]__ - Will load all Hyperlambda files from a specified folder, and treat these as widgets.
 * - __[files]__ - Will load all files specified as children arguments, and treat these as widgets
 * - __[widgets]__ - A plain collection of widgets
 *
 * All other arguments are passed in _"as is"_. The arguments above are handled in
 * order of appearance, allowing you to at least to some extent control the order
 * of your widgets. All arguments beneath each file, inside your __[files]__ argument,
 * are appended to the specified files after loading. All arguments to your __[folder]__
 * argument, are appended into all files that are loaded from your folder. The __[files]__
 * argument expects you to supply all files you want to load as the name parts of your
 * files collection.
 */
create-event:micro.widgets.file

  /*
   * Used to separate arguments from the rest of our lambda.
   */
  .signal

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.optional:x:/..
    folder:string
    files
    widgets

  /*
   * Making sure we get [widgets], [files] and [folder] widgets added in order
   * specified by caller.
   */
  for-each:x:/@.signal/--/<-
    switch:x:/@_dp/#?name
      case:widgets

        /*
         * Adding widgets.
         */
        add:x:/../*/return/*/container/*/widgets
          src:x:/@_dp/#/*

      case:files

        /*
         * Loading all Hyperlambda files listed beneath [files] as names,
         * making sure we parametrize each file according to arguments supplied
         * by caller.
         */
        for-each:x:/@_dp/#/*
          load-file:x:/@_dp/#?name

          /*
           * Adding all arguments beneath currently iterated node.
           */
          add:x:/@load-file/*/*
            src:x:/@_dp/#/*

          /*
           * Adding widgets.
           */
          add:x:/../*/return/*/container/*/widgets
            src:x:/@load-file/*/*

      case:folder

        /*
         * Loading all Hyperlambda files from specified [folder].
         */
        list-files:x:/@_dp/#?value
          filter:.hl
        load-file:x:/@list-files/*?name

        /*
         * Adding all arguments beneath currently iterated node.
         */
        add:x:/@load-file/*/*
          src:x:/@_dp/#/*

        /*
         * Adding widgets.
         */
        add:x:/../*/return/*/container/*/widgets
          src:x:/@load-file/*/*

  /*
   * Adding all other arguments to returned container widget.
   */
  add:x:/../*/return/*/container
    src:x:/@.signal/--/<-(!/_arg!/folder!/files!/widgets)

  /*
   * Returning extension widget to caller.
   */
  return
    container
      widgets
