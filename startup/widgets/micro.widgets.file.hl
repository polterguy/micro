
/*
 * An extension widget that will automatically load all Hyperlambda files
 * from the given [folder], and append these as children to itself.
 *
 * Pass in [folder] and/or [files] to declare which files or folder you wish to
 * load your plugins from.
 * All other arguments are passed in "as is".
 */
create-event:micro.widgets.file

  /*
   * Used to separate arguments from the rest of our lambda.
   */
  .signal

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.optional:x:/..
    folder:string
    files

  /*
   * Loading all Hyperlambda files from specified [folder].
   */
  list-files:x:/@.signal/--/folder?value
    filter:.hl
  load-file:x:/@list-files/*?name
  load-file:x:/../*/files/*?name

  /*
   * Adding all files as widgets into return container widget's widgets collection.
   */
  add:x:/../*/return/*/container/*/widgets
    src:x:/../*/load-file/*/*

  /*
   * Adding all other arguments to returned container widget.
   */
  add:x:/../*/return/*/container
    src:x:/@.signal/--/<-(!/_arg!/folder!/files)

  /*
   * Returning extension widget to caller.
   */
  return
    container
      widgets
