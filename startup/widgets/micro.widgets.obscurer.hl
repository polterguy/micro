
/*
 * Creates the modal "obscurer" widget, which is an Ajax Wait type of widget.
 *
 * Pass in [message] to explicitly choose which text to display to user as he waits.
 * If you don't pass in a [message] argument, the default will be "Please wait ...".
 * All other arguments are passed in "as is".
 *
 * Notice, if you pass in a [class] and/or a [widgets] collection, you will override
 * the entire default behavior of these elements, and you're on your own in regards
 * to making sure your logic becomes accurate in these regards.
 */
create-event:micro.widgets.obscurer

  /*
   * Used to separate parameters from the rest of the event.
   */
  .signal

  /*
   * Defaults.
   */
  .defaults
    message:Please wait ...

  /*
   * Adding all specified arguments into return widget.
   */
  add:x:/../*/return/*/container
    src:x:/@.signal/--(!/message)

  /*
   * Checking if caller specified a widgets collection, and if not, making
   * sure we explicitly add one.
   */
  if:x:/../*/return/*/container/*/widgets
    not

    /*
     * Making sure container widget has a [widgets] collection, since caller did
     * not specify one.
     */
    eval-x:x:/+/**/innerValue
    add:x:/../*/return/*/container
      src
        widgets
          h3
            class:micro-ajax-wait-info
            innerValue:x:/../*(/message|/.defaults/*/message)/$?value
          div
            class:micro-ajax-wait-animations
            widgets

  /*
   * Checking if caller specified a [class], and if not, making sure we 
   * explicitly add one.
   */
  if:x:/../*/return/*/container/*/class
    not

    /*
     * Adding default [class] declaration.
     */
    add:x:/../*/return/*/container
      src
        class:micro-ajax-wait

  /*
   * Adding the "Ajax wait" animations into return widget's [widgets] collection.
   */
  add:x:/../*/return/*/container/*/widgets/*/div/*/class/=micro-ajax-wait-animations/./*/widgets
    src
      literal
        element:div
        class:micro-ajax-square micro-ajax-square-1
      literal
        element:div
        class:micro-ajax-square micro-ajax-square-2
      literal
        element:div
        class:micro-ajax-square micro-ajax-square-3
      literal
        element:div
        class:micro-ajax-square micro-ajax-square-4
      literal
        element:div
        class:micro-ajax-square micro-ajax-square-5
      literal
        element:div
        class:micro-ajax-square micro-ajax-square-6
      literal
        element:div
        class:micro-ajax-square micro-ajax-square-7
      literal
        element:div
        class:micro-ajax-square micro-ajax-square-8

  /*
   * Returning widget to caller.
   */
  return
    container
