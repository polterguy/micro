/*
 * Creates the __[micro.widgets.cover]__ extension widget.
 */

/*
 * This widget creates a _"cover"_ widget, which will cover the entire browser
 * surface with a semi transparent background, preventing interaction with any
 * other widgets on your page - At least with your mouse.
 *
 * Optionally pass in a __[message]__ argument to explicitly choose which text
 * to display to the user. If you don't pass in an explicit __[message]__ argument,
 * the default value will be _"Please wait ..."_. All other arguments are passed
 * in as is to the main widget.
 */
create-event:micro.widgets.cover

  /*
   * Used to separate parameters from the rest of the event.
   */
  .signal

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.optional:x:/..
    message:string

  /*
   * Defaults.
   */
  .defaults
    message:Please wait ...
    class:micro-widgets-cover

  /*
   * Adding all specified arguments into return widget.
   */
  add:x:/../*/return/*/container
    src:x:/@.signal/--(!/message!/class)

  /*
   * Adding class.
   */
  add:x:/../*/return/*/container
    src:x:(/@.signal/--/class|/@.defaults/*/class)/$

  /*
   * Returning widget to caller.
   */
  eval-x:x:/+/*/*/widgets/*/h1/*/innerValue
  return
    container
      widgets
        h1
          innerValue:x:(/@.signal/--/message|/@.defaults/*/message)/$?value
        div
          widgets
            literal
              element:span
            literal
              element:span
            literal
              element:span
            literal
              element:span
            literal
              element:span
            literal
              element:span
            literal
              element:span
            literal
              element:span
