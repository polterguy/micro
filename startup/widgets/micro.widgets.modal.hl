/*
 * Creates the __[micro.widgets.modal]__ extension widget.
 */

/*
 * This extension widget allows you to create a _"modal window"_, which prevents
 * the user from interacting with any widgets on the page, besides the widgets
 * that are inside of your modal window.
 *
 * It requires a __[widgets]__ collection, being a bunch of widgets that will
 * occupy the widget's main surface. All other arguments will be passed in
 * _"as is"_, which implies that they'll be added to the cover widget, which
 * prevents the user from interacting with other widgets on your page.
 */
create-event:micro.widgets.modal

  /*
   * Used to separate parameters from the rest of the event.
   */
  .signal

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.min:x:/..
    widgets

  /*
   * Adding all [widgets] into container widget for modal window.
   */
  add:x:/../*/return/**/widgets/=widgets
    src:x:/@.signal/--/widgets/*

  /*
   * Adding the rest of the arguments into return widget.
   */
  add:x:/../*/return/*/container
    src:x:/@.signal/--(!/widgets)

  /*
   * Checking if caller supplied a [class] argument, and if not, defaulting
   * to the default class for widget.
   */
  if:x:/../*/class
    not
    add:x:/../*/return/*/container
      src
        class:micro-widgets-modal

  /*
   * Returning widget to caller.
   */
  eval-x:x:/+/*/*/onclick
  return
    container

      // Hack to make sure window doesn't flicker during initial display.
      style:"display:none;"
      data-window:modal
      onclick:@"if (event.currentTarget == event.target) {event.target.style.display='none';event.target.className='nothing';p5.$(event.target.id).raise('.ondelete');}"
      .ondelete
        delete-widget:x:/../*/_event?value
      oninit
        p5.web.send-javascript:@"
document.body.classList.add ('micro-body-open');
p5.$('{0}').el.addEventListener ('DOMNodeRemoved', function(e){{
  if (e.target.id == '{0}') {{
    if(document.querySelectorAll('[data-window=""modal""]').length == 1) {{
      document.body.classList.remove('micro-body-open');
    }}
  }}
}});"
          :x:/../*/_event?value
      widgets
        container
          class:micro-widgets-modal-content
          widgets
            container
              widgets:widgets
