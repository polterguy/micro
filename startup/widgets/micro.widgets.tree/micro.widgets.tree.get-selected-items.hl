/*
 * Creates the __[micro.widgets.tree.get-selected-items]__ Active Event.
 */

/*
 * This event allows you to retrieve the currently selected item(s) from a
 * specified __[\_arg] [micro.widgets.tree]__ widget. __[\_arg]__ must be the
 * ID of your tree widget.
 *
 * The event will return an __[items]__ collection, where the name of the nodes,
 * are the IDs of all items that are currently selected in your tree.
 */
create-event:micro.widgets.tree.get-selected-items

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    _arg:string

  /*
   * This is the tree caller requested to retrieve selected items in.
   * Finding all items in the tree that are currently selected.
   */
  p5.web.widgets.find-like:x:/../*/_arg?value
    class:tree-selected
  if:x:/@p5.web.widgets.find-like/*/*?count
    !=:int:0

    /*
     * Tree has selected items.
     * Finding its parent "li" widget, and retrieving its [_item-id], and returning as [items] collection to caller.
     */
    p5.web.widgets.get-parent:x:/@p5.web.widgets.find-like/*/*?value
    p5.web.widgets.property.get:x:/@p5.web.widgets.get-parent/*/*?value
      _item-id
    add:x:/+/*
      src:x:/@p5.web.widgets.property.get/*/*?value
    return
      items
