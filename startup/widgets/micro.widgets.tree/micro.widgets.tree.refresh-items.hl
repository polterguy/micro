/*
 * Creates the __[micro.widgets.tree.refresh-items]__ Active Event.
 */

/*
 * This event allows you to _"refresh"_ children items in the specified
 * __[\_arg] [micro.widgets.tree]__ widget. __[\_arg]__ must be the ID of
 * your main tree widget.
 *
 * This event will delete all specified __[items]__ from your widget,
 * and re-invoke your __[.onexpand]__ lambda for these items, to
 * re-retrieve them, and re-create that particular level in your tree
 * view.
 */
create-event:micro.widgets.tree.refresh-items

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    _arg:string
    items

  /*
   * Looping through all [items] and re-fetches them.
   */
  for-each:x:/../*/items/*?name

    /*
     * Finding currently iterated item, and making sure we de-expand it.
     */
    p5.web.widgets.find-first:x:/../*/_arg?value
      _item-id:x:/@_dp?value

    /*
     * Deleting its old "ul" widget.
     */
    p5.web.widgets.find-first:x:/@p5.web.widgets.find-first/*/*?value
      element:ul
    delete-widget:x:/-/*/*?value

    /*
     * Toggling item, making sure we do a [force-expand].
     */
    add:x:/..for-each/*/micro.widgets.tree.toggle-items/*/items
      src:x:/@_dp?value
    micro.widgets.tree.toggle-items:x:/../*/_arg?value
      force-expand:bool:true
      items

    /*
     * Checking if we should remove "tree-leaf" class from item.
     */
    p5.web.widgets.find-first:x:/..for-each/*/p5.web.widgets.find-first/[0,1]/*/*?value
      element:ul
    if:x:/-/*/*?value

      /*
       * Removing "tree-leaf" class on "li" widget.
       */
      micro.css.delete:x:/..for-each/*/p5.web.widgets.find-first/[0,1]/*/*?value
        class:tree-leaf
