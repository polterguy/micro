
/*
 * Creates the __[micro.page.minify-and-bundle-javascript-file]__ Active Event.
 */

/*
 * This event will minify and bundle all specified JavaScript files, and include
 * the minified and bundled JS file on your page.
 *
 * All bundles created this way, will be stored in your
 * _"/common/documents/public/micro-javascript-cache/"_ folder.
 *
 * The event will determine what filename to use for your bundle by doing a hash
 * on your filenames, and check if there already exists a cached bundled file,
 * before performing the actual bundling and minification - Implying that it
 * will only bundle and minify the same arguments it is given _once_, making it
 * fairly fast in terms of execution speed.
 */
create-event:micro.page.minify-and-bundle-javascript-file

  /*
   * Signal node, to separate the arguments from the rest of our lambda.
   *
   * Also used to store all CSS file, as names of children nodes.
   */
  .signal

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    _arg:string

  /*
   * Looping through each CSS file supplied as arguments, and appending into
   * above [.signal] node.
   *
   * Notice, we explicitly unroll the path, before we use it to determine the hash value.
   */
  for-each:x:/../*/_arg?value
    add:x:/@.signal
      p5.io.unroll-path:x:/@_dp?value

  /*
   * Creating a SHA1 hash from all file path arguments, to have a unique filename for
   * our bundled CSS file.
   */
  p5.crypto.hash.create-sha1:x:/@.signal/*?name
    hex:true

  /*
   * Checking that our wrapper "bundle cache" folder exists, and creating it if it doesn't.
   */
  if
    fetch:x:/0/0?value
      folder-exists:/common/documents/public/micro-javascript-cache/
    not

    /*
     * Creating JavaScript cache folder.
     */
    create-folder:/common/documents/public/micro-javascript-cache/

  /*
   * Checking if wrapper file has already been created (and cached).
   */
  if
    fetch:x:/0/0?value
      file-exists:/common/documents/public/micro-javascript-cache/bundle-{0}.js
        :x:/@p5.crypto.hash.create-sha1?value
    not

    /*
     * Loading and minifying all JavaScript files, bundling the result into a single file, 
     * and saving it, using the hash value from above to get a unique filename for each
     * distinct bundle.
     */
    load-file:x:/@.signal/*?name

    /*
     * Minifying JavaScript content, joining everything into one single file, and saving
     * the results to our "/common/" folder.
     */
    p5.web.javascript.minify:x:/@load-file/*?value
    join:x:/@p5.web.javascript.minify/*?value
      sep:";"
    save-file:/common/documents/public/micro-javascript-cache/bundle-{0}.js
      :x:/@p5.crypto.hash.create-sha1?value
      src:x:/@join?value

  /*
   * Including minified (and bundled) resulting single JavaScript file on page.
   */
  p5.web.include-javascript-file:/common/documents/public/micro-javascript-cache/bundle-{0}.js
    :x:/@p5.crypto.hash.create-sha1?value
