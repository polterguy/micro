
/*
 * Creates the __[micro.windows.info]__ Active Event.
 */

/*
 * This event creates a small _"information bubble"_ window, that will be
 * displayed on the page for some few seconds, before automatically disappearing.
 *
 * Pass in text to display as __[\_arg]__.
 * Optionally pass in __[parent]__ as the parent widget and __[class]__ to
 * override its default CSS class.
 */
create-event:micro.windows.info

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    _arg:string
  micro.lambda.contract.optional:x:/..
    parent:string
    class:string

  /*
   * Deleting any previously created information bubbles.
   */
  if
    fetch:x:/0/0?value
      widget-exists:micro-windows-info
    delete-widget:micro-windows-info

  /*
   * Adding [parent] if specified.
   */
  add:x:/../*/create-widget
    src:x:/../*/parent
  if:x:/../*/parent

    /*
     * Since user gave us an explicit parent, we assume it was to move the positioning
     * of the window around, and hence we change its "position" from "fixed" to "absolute".
     */
    add:x:/../*/create-widget
      src
        style:"position:absolute;"

  /*
   * Checking if user gave us an explicit [class].
   */
  if:x:/../*/class
    add:x:/../*/create-widget
      src:x:/../*/class
  else
    add:x:/../*/create-widget
      src
        class:micro-windows-info

  /*
   * Checking if user gave us an explicit [onclick].
   */
  if:x:/../*/onclick/*
    add:x:/../*/create-widget/*/onclick/*/.lambda
      src:x:/../*/onclick/*

  /*
   * Creating new information bubble window.
   */
  create-widget:micro-windows-info
    role:button
    onclick

      /*
       * Making sure we invoke any user provided lambda callbacks.
       */
      .lambda
      eval:x:/-

      /*
       * Deleting widget.
       */
      delete-widget:micro-windows-info

    widgets
      label
        innerValue:x:/../*/_arg?value
