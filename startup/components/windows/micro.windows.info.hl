
/*
 * Creates a small "information bubble" window, that will be displayed on the form for n seconds, 
 * before automatically disappearing.
 *
 * Pass in text to display as [_arg].
 * Optionally pass in [parent] as parent widget to where to display window, [class] to override default 
 * class or [onclick] to provide a user defined lambda callback that will be invoked when user clicks the bubble window.
 *
 * Alternatively, you can also pass in a [widgets] collection, which will be appended into the information bubble as is.
 */
create-event:micro.windows.info

  /*
   * Deleting any previously created information bubbles.
   */
  if
    fetch:x:/0/0?value
      widget-exists:micro-windows-info
    delete-widget:micro-windows-info

  /*
   * Adding [parent] if specified.
   */
  add:x:/../*/create-widget
    src:x:/../*/parent
  if:x:/../*/parent

    /*
     * Since user gave us an explicit parent, we assume it was to move the positioning
     * of the window around, and hence we change its "position" from "fixed" to "absolute".
     */
    add:x:/../*/create-widget
      src
        style:"position:absolute;"

  /*
   * Checking if user gave us an explicit [class].
   */
  if:x:/../*/class
    add:x:/../*/create-widget
      src:x:/../*/class
  else
    add:x:/../*/create-widget
      src
        class:micro-windows-info

  /*
   * Checking if user gave us an explicit [onclick].
   */
  if:x:/../*/onclick/*
    add:x:/../*/create-widget/*/onclick/*/.lambda
      src:x:/../*/onclick/*

  /*
   * Adding custom widgets to information bubble.
   */
  add:x:/../*/create-widget/*/widgets
    src:x:/../*/widgets/*

  /*
   * Creating new information bubble window.
   */
  create-widget:micro-windows-info
    role:button
    onclick

      /*
       * Making sure we invoke any user provided lambda callbacks.
       */
      .lambda
      eval:x:/-

      /*
       * Deleting widget.
       */
      delete-widget:micro-windows-info

    widgets
      span
        innerValue:x:/../*/_arg?value
