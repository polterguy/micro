
/*
 * Creates a window that allows the user to change his password
 */
create-event:micro.windows.change-password

  /*
   * Sanity check, only logged in users can change their passwords.
   */
  whoami
  if:x:/@whoami/*/default?value
    throw:You're not logged in

  /*
   * Creates modal window, allowing user to change his password.
   */
  create-widgets
    micro.widgets.modal:micro-change-password
      widgets
        h3
          innerValue:Please supply a new password
        micro.widgets.wizard-form:micro-change-password-data
          text:micro-change-password-pwd
            info:Password
            .data-field:password
            type:password
            onkeydown:@"if (event.keyCode == 13) {p5.$('micro-change-password-ok').raise('onclick');return false;}"
            oninit

              /*
               * Setting initial focus to widget.
               */
              micro.page.set-focus:x:/../*/_event?value

          text:micro-change-password-pwd-repeat
            info:Repeat
            .data-field:repeat
            type:password
            onkeydown:@"if (event.keyCode == 13) {p5.$('micro-change-password-ok').raise('onclick');return false;}"

        div
          class:right
          widgets
            div
              class:strip
              widgets
                button:micro-change-password-ok
                  innerValue:Save
                  onclick

                    /*
                     * Retrieving wizard form's data and updating password.
                     */
                    micro.widgets.wizard-form.value:micro-change-password-data

                    /*
                     * Sanity checking passwords given by user.
                     */
                    if:x:/@micro.widgets.wizard-form.value/*/password?value
                      =:
                      or:x:/@micro.widgets.wizard-form.value/*/password?value
                        !=:x:/@micro.widgets.wizard-form.value/*/repeat?value

                      /*
                       * Oops, bad data, informing user and returning early.
                       */
                      p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                        class:micro-widgets-modal-content
                      eval-x:x:/+/*
                      micro.windows.info:You must supply your password twice, and the values must match
                        class:micro-windows-info warning
                        parent:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                      return

                    /*
                     * User typed in a password, and the passwords did match.
                     */
                    p5.auth.misc.change-my-password:x:/@micro.widgets.wizard-form.value/*/password?value

                    /*
                     * Giving the user feedback that the operation was a success,
                     * and deleting the modal widget.
                     */
                    micro.windows.info:Password was successfully changed
                      class:micro-windows-info success
                    delete-widget:micro-change-password

                button
                  innerValue:Close
                  onclick

                    /*
                     * Simply deleting widget.
                     */
                    delete-widget:micro-change-password
