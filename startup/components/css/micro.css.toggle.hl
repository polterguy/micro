/*
 * Creates the __[micro.css.toggle]__ Active Event.
 */

/*
 * This event allows you to toggle CSS class(es) on one or more widgets.
 *
 * Pass in __[\_arg]__ as widget ID(s) and __[class]__ as CSS class(es)
 * you want to toggle on widget. If any of the class(es) you pass in exists
 * from before on widget, they will be removed - If they don't exist, they
 * will be added.
 */
create-event:micro.css.toggle

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.min:x:/..
    class:string
  micro.lambda.contract.optional:x:/..
    _arg:string

  /*
   * Splitting upon every given CSS class caller wants to toggle.
   */
  split:x:/../*/class?value
    =:,
    =:" "
    trim:true

  /*
   * Retrieving CSS class of specified widget.
   */
  get-widget-property:x:/../*/_arg?value
    class

  /*
   * Splitting each CSS class already in widget, such that we can figure out if class(es)
   * caller wants to toggle already exists or not.
   */
  split:x:/@get-widget-property/*/*?value
    =:" "

  /*
   * Looping through each class supplied.
   */
  for-each:x:/../*/split/[0,1]/*/$

    /*
     * Checking if currently iterated CSS class is in [split] result.
     */
    if:x:/@split/*/{0}
      :x:/@_dp/#?name

      /*
       * Class exists, deleting it.
       */
      set:x:/@split/*/{0}
        :x:/@_dp/#?name

    else

      /*
       * Class does NOT exist, adding it.
       */
      add:x:/@split
        src:x:/@_dp/#?name

  /*
   * Invoking [join] on [split] results, before setting new class value.
   */
  join:x:/@split/*?name
    sep:" "
  set-widget-property:x:/../*/_arg?value
    class:x:/@join?value
