
/*
 * Creates a small "information bubble" window, that will be displayed on the form for n seconds, 
 * before automatically disappearing.
 *
 * Pass in text to display as [_arg].
 * Optionally pass in [parent] as parent widget to where to display window.
 */
create-event:micro.windows.info

  /*
   * Making sure we include extension CSS file.
   */
  p5.web.include-css-file:@MICRO/media/ext.css

  /*
   * Deleting any previously created information bubbles.
   */
  if
    fetch:x:/0/0?value
      widget-exists:micro-windows-info
    delete-widget:micro-windows-info

  /*
   * Adding [parent] if specified.
   */
  add:x:/../*/create-widget
    src:x:/../*/parent

  /*
   * Checking if user gave us an explicit [class].
   */
  if:x:/../*/class
    add:x:/../*/create-widget
      src:x:/../*/class
  else
    add:x:/../*/create-widget
      src
        class:micro-windows-info

  /*
   * Creating new information bubble window.
   */
  create-widget:micro-windows-info
    innerValue:x:/../*/_arg?value
    role:button
    onclick
      delete-widget:micro-windows-info
