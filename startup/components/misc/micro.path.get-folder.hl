/*
 * Creates the __[micro.path.get-folder]__ Active Event.
 */

/*
 * This event allows you to return only the folder parts from a given path.
 *
 * The event will strip away the filename from a specified path, if there is a file part,
 * and return only the folder of the given path.
 */
create-event:micro.path.get-folder

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    _arg:string

  /*
   * First checking if path already is a folder.
   */
  index-of:x:/../*/_arg?value
    src:/
  +:x:/@index-of/0/-?value
    _:1
  length:x:/../*/_arg?value
  if:x:/@+?value
    =:x:/@length?value

    /*
     * Path is already a reference to a folder.
     */
    return:x:/../*/_arg?value

  /*
   * Splitting up components of path, removing the last entity,
   * joining results, and returning to caller.
   */
  split:x:/../*/_arg?value
    =:/
  set:x:/@split/0/-
  join:x:/@split/*?name
    sep:/
  return:/{0}/
    :x:/@join?value