
/*
 * Creates the event that allows caller to translate a piece of text from one
 * language to another, using Google Translate.
 *
 * Pass in __[\_arg]__ as whatever you want to have translated, and __[dest-lang]__
 * being the language you wish to have it translated to.
 *
 * Optionally pass in __[src-lang]__, for explicitly declaring what language your
 * source text is in. If no __[src-lang]__ is specified, then _"auto"_ will be
 * assumed, at which point Google will attempt to intelligently figure out what
 * your source language actually is.
 *
 * Example of some languages you could use are; _"en"_ English, and _"no"_ for
 * Norwegian, or _"nb-NO"_ for Norwegian Bokmål and _"en-US"_ for American English.
 */
create-event:micro.google.translate

  /*
   * Defaults.
   */
  .defaults
    src-lang:auto

  /*
   * Sanity check.
   */
  micro.lambda.contract.min:x:/..
    dest-lang:string

  /*
   * URL encoding text.
   */
  p5.html.url-encode:x:/../*/_arg?value

  /*
   * Invoking Google Translate with URL encoded text, source language, and destination language.
   */
  p5.http.get:"https://translate.googleapis.com/translate_a/single?client=gtx&ie=UTF-8&sl={0}&tl={1}&dt=t&q={2}"
    :x:(/../*/src-lang|/@.defaults/*/src-lang)/$?value
    :x:/../*/dest-lang?value
    :x:/@p5.html.url-encode?value

  /*
   * Google Translate will return results as JSON, making sure we parse it as such,
   * returning it to caller as a single piece of text.
   */
  json2lambda:x:/@p5.http.get/**/content?value

  /*
   * Returning result to caller.
   */
  join:x:/@json2lambda/0/0/*/0?value
  return:x:/@join?value
