
/*
 * Will showcase all the extended widgets in Micro.
 */


/*
 * Includes the main CSS for Micro on page.
 */
p5.web.include-css-file:@MICRO/media/main.css


/*
 * Clears container for any previous widgets.
 */
clear-widget:cnt


/*
 * Creates our fullscreen container first, for holding our navbar.
 */
create-widget:content-menu
  class:container-fullscreen
  events

    /*
     * Helper event to have som visual feedback when menu items are clicked.
     */
    micro.samples.show-modal
      create-widgets
        micro.widgets.modal:menu-modal
          class:micro-modal
          widgets
            h3
              innerValue:Menu widget
            p
              innerValue:You just clicked a menu item
            div
              class:right
              widgets
                button
                  innerValue:Close
                  oninit
                    micro.page.set-focus:x:/../*/_event?value
                  onclick
                    delete-widget:menu-modal

  widgets
    micro.widgets.menu
      items
        Show modal
          onclick
            micro.samples.show-modal
        Dropdown 1
          items
            GitHub for P5
              href:"https://github.com/polterguy/phosphorusfive"
              target:_blank
            Click me!:some-item
              onclick
                set-widget-property:some-item
                  innerValue:I was clicked
            Sub items
              items
                My blog
                  href:"https://gaiasoul.com"
                  target:_blank
                Shows modal
                  onclick
                    micro.samples.show-modal


/*
 * Creates a default container.
 */
create-container-widget:content
  class:container


/*
 * Creates the main header and information.
 */
create-widget
  parent:content
  class:row
  widgets
    div
      class:col
      widgets
        h1
          innerValue:Micro extended widgets
        p
          innerValue:@"These are the 'extended' widgets in Micro, such as modal widgets, tab widgets, etc."


/*
 * Modal window widget example.
 */
create-widget
  parent:content
  class:row prepend-top
  widgets
    div
      class:col
      widgets
        h3
          innerValue:Modal widget
        p
          innerValue:Below is an example of a modal widget. When you click the button below, a modal widget will be created and shown.
        button:show-modal-button
          innerValue:Show modal
          onclick

            /*
             * Creating our modal widget.
             */
            create-widgets
              micro.widgets.modal:modal-widget-2
                class:micro-modal
                widgets
                  h3
                    innerValue:Modal widget
                  p
                    innerValue:@"Notice, there is no custom JavaScript for the modal widget in Micro.
By intelligently combining CSS with the Managed Ajax parts of P5, we're able to get away with creating a modal widget, 
without adding any overhead to our page at all."
                  p
                    innerValue:@"Which is in stark contrast to Bootstrap, that requires jQuery and hundreds
of kilobytes of custom JavaScript and CSS to display a simple modal window."
                  p
                    innerValue:@"You can put any other widgets you wish into the modal widget in Micro."
                  div
                    class:right
                    widgets
                      button
                        innerValue:Close
                        oninit
                          micro.page.set-focus:x:/../*/_event?value
                        onclick
                          delete-widget:modal-widget-2
        


/*
 * Tab widget example.
 */
create-widget
  parent:content
  class:row prepend-top
  widgets
    div
      class:col-100
      widgets
        h3
          innerValue:Tab widget
        p
          innerValue:Below is an example of a tab widget.
    div
      class:col-100
      widgets
        micro.widgets.tab
          class:micro-tab
          view
            name:First
            widgets
              h4
                innerValue:First view
              p
                innerValue:This is the first view in your tab.
          view
            name:Second
            widgets
              h4
                innerValue:Second view
              p
                innerValue:@"This is the second view in your tab. By default, there are no borders around the tab widget, since this
would imply having to create padding for its content, which would move the text layout outwards into the page, making it not align nicely
with the rest of your content. If you wish, this is easily added with a single CSS class addition to the main tab widget."
          view
            name:Third
            widgets
              h4
                innerValue:Third view
              p
                innerValue:@"This is the third view in your tab. You can add any other widgets you wish into your views.
Below is an example of a button."
              div
                class:right
                widgets
                  button
                    innerValue:Click me!
                    onclick
                      set-widget-property:x:/../*/_event?value
                        innerValue:I was clicked
    div
      class:col-100 prepend-top
      widgets
        micro.widgets.tab
          class:micro-tab micro-tab-border
          view
            name:First
            widgets
              p
                innerValue:@"This tab widget has the 'micro-tab-border' CSS class added to it, which makes it have a border, 
making it more easily seen as a single component on your page."
          view
            name:Second
            widgets
              p
                innerValue:@"This makes your tab widget look more similar to traditional tabs, but also unfortunately aligns its
content not in accordance with the rest of the content on your page. Which you prefer, is probably a matter of taste."
