
/*
 * Will showcase all the extended widgets in Micro.
 */


/*
 * Includes the main CSS for Micro on page.
 */
p5.web.include-css-file:@MICRO/media/main.css


/*
 * Clears container for any previous widgets.
 */
clear-widget:cnt


/*
 * Creates our fullscreen container first, for holding our navbar.
 */
create-widget:content-menu
  class:container-fullscreen
  events

    /*
     * Helper event to have som visual feedback when menu items are clicked.
     */
    micro.samples.show-modal
      create-widgets
        micro.widgets.modal:menu-modal
          class:micro-modal
          widgets
            h3
              innerValue:Menu widget
            p
              innerValue:You just clicked a menu item
            div
              class:right
              widgets
                button
                  innerValue:Close
                  oninit
                    micro.page.set-focus:x:/../*/_event?value
                  onclick
                    delete-widget:menu-modal

  widgets
    micro.widgets.menu
      items
        Menu widget
          onclick
            micro.samples.show-modal
        Dropdown
          items
            GitHub for P5
              href:"https://github.com/polterguy/phosphorusfive"
              target:_blank
            .separator
            Click me!:some-item
              onclick
                set-widget-property:some-item
                  innerValue:I was clicked
            Sub items
              items
                My blog
                  href:"https://gaiasoul.com"
                  target:_blank
                Shows modal
                  onclick
                    micro.samples.show-modal


/*
 * Creates a default container.
 */
create-container-widget:content
  class:container
  widgets
    a
      href:/
      innerValue:Home
      style:"position:absolute;right:15px;"


/*
 * Creates the main header and information.
 */
create-widget
  parent:content
  class:row
  widgets
    div
      class:col
      widgets
        h1
          innerValue:Micro extended widgets
        p
          innerValue:@"These are the 'extended' widgets in Micro, such as modal widgets, tab widgets, etc. To see how they work in combination
with your favourite skin, feel free to select a skin from the select dropdown box below."
    div
      class:col-100
      widgets
        micro.widgets.skin-selector


/*
 * Modal window widget example.
 */
create-widget
  parent:content
  class:row air-top
  widgets
    div
      class:col
      widgets
        h3
          innerValue:Modal widget
        p
          innerValue:Below is an example of a modal widget. When you click the button below, a modal widget will be created and shown.
        button:show-modal-button
          innerValue:Show modal
          onclick

            /*
             * Creating our modal widget.
             */
            create-widgets
              micro.widgets.modal:modal-widget-2
                class:micro-modal
                widgets
                  h3
                    innerValue:Modal widget
                  p
                    innerValue:@"Notice, there is no custom JavaScript for the modal widget in Micro.
By intelligently combining CSS with the Managed Ajax parts of P5, we're able to get away with creating a modal widget, 
without adding any overhead to our page at all. Which is in stark contrast to Bootstrap, that requires jQuery and hundreds
of kilobytes of custom JavaScript and CSS to display a simple modal window. You can put any widget you wish into the modal widget in Micro.
Here we have added a button that simply closes our modal widget."
                  div
                    class:right
                    widgets
                      button
                        innerValue:Close
                        oninit
                          micro.page.set-focus:x:/../*/_event?value
                        onclick
                          delete-widget:modal-widget-2
        


/*
 * Tab widget example.
 */
create-widget
  parent:content
  class:row air-top
  widgets
    div
      class:col-100
      widgets
        h3
          innerValue:Tab widget
        p
          innerValue:Below is an example of a tab widget.
    div
      class:col-100
      widgets
        micro.widgets.tab
          class:micro-tab
          view
            name:#1
            widgets
              h4
                innerValue:First view
              p
                innerValue:This is the first view in your tab.
          view
            name:#2
            widgets
              h4
                innerValue:Second view
              p
                innerValue:@"This is the second view in your tab. You can add any other widgets you wish into your views.
Below is an example of a button."
              div
                class:right
                widgets
                  button
                    innerValue:Click me!
                    onclick
                      set-widget-property:x:/../*/_event?value
                        innerValue:I was clicked
    div
      class:col-100 air-top
      widgets
        micro.widgets.tab
          class:micro-tab micro-tab-border
          view
            name:#1
            widgets
              p
                innerValue:@"This tab widget has the 'micro-tab-border' CSS class added to it, which makes it have a border, 
making it more easily seen as a single component on your page. In addition, some of the skins in Micro, might choose to further
add design elements to the 'micro-tab-border' class, making it pop out."
          view
            name:#2
            widgets
              p
                innerValue:@"This makes your tab widget look more similar to traditional tabs, but also unfortunately aligns its
content not in accordance with the rest of the content on your page. Which you prefer, is probably a matter of taste."



/*
 * Wizard form widget example.
 */
create-widget
  parent:content
  class:row air-top
  widgets
    div
      class:col-100
      widgets
        h3
          innerValue:Wizard form widget
        p
          innerValue:@"A wizard form widget allows you to easily create rich forms declaratively, by simply supplying the type
of data you want to collect, and leave the creation of the actual widgets up to the wizard form Active Event. Supported widgets are as follows."
        ul
          innerValue:@"
<li><strong>[text]</strong> - Single line text input - Expects [info] argument</li>
<li><strong>[textarea]</strong> - Multiline text input - Expects [info] argument</li>
<li><strong>[select]</strong> - Select dropdown list - Expects [info] argument</li>
<li><strong>[checkbox]</strong> - Checkbox widget - Expects [info] argument</li>
<li><strong>[radio-group]</strong> - Grouped radio buttons</li>"
        p
          innerValue:@"All other arguments will be appended as is, allowing you to create for instance additional button widgets and such,
making it possible to clear the form, create spacing, horizontal rulers, collect the user's data, etc. In addition, each form can take
an optional <strong>[class]</strong> argument, signifying which root class to use for the form. The form below has the 
classes <em>'bg inner-air rounded shaded'</em> associated with it."

    /*
     * Our actual wizard form.
     */
    div
      class:col-70
      widgets
        micro.widgets.wizard-form:my-form
          class:bg air-inner rounded shaded
          text:text-widget
            info:Some text widget
          label
            for:textarea-widget
            innerValue:Some textarea
          textarea:textarea-widget
            info:Some multiline text ...
          checkbox:checkbox-widget-1
            info:Some checked checkbox
            checked
          br
          checkbox:checkbox-widget-2
            info:Another checkbox
          select:select-widget
            info:Some select widget
            options
              Option 1:option-1
              Option 2:option-2
          radio-group:radio-buttons-group
            options
              Radio 1:radio-widget-1
              Radio 2:radio-widget-2
                checked
              Radio 3:radio-widget-3
          input:hidden-value
            type:hidden
            value:Hidden value!
          br
          p
            innerValue:"This is a 'wizard form', which allows you to easily declare a form input type of widget."
          div
            class:right
            widgets
              button
                innerValue:Serialize
                onclick

                  /*
                   * Displaying results in a modal widget.
                   */
                  micro.widgets.wizard-form.value:my-form
                  lambda2hyper:x:/-/*
                  eval-x:x:/+/**/pre/*/innerValue
                  create-widgets
                    micro.widgets.modal:form-result
                      class:micro-modal
                      widgets
                        h3
                          innerValue:Result
                        pre
                          innerValue:x:/@lambda2hyper?value
                        div
                          class:right
                          widgets
                            button
                              innerValue:Close
                              oninit
                                micro.page.set-focus:x:/../*/_event?value
                              onclick
                                delete-widget:form-result
    div
      class:col-30 rounded shaded air-inner emphasized
      widgets
        p
          innerValue:@"This form is wrapped inside a 'col-70' responsive column, which means you can resize the browser below 800px,
and the form will automatically pop out and fill the whole viewport."
        p
          innerValue:@"Try to resize your browser below 800px in width to see this effect."
        p
          innerValue:@"Most of the widgets in the form requires an <strong>[info]</strong> argument, which will be used as a label, title, etc - Depending
upon the type of widget you create."



/*
 * Grid widget example explanation.
 */
create-widget
  parent:content
  class:row air-top
  widgets
    div
      class:col-100
      widgets
        h3
          innerValue:Grid widget
        p
          innerValue:@"A grid widget allows you to display tabular data in a table. It is also commonly referred to as a 'datagrid'.
It's consumed through the <strong>[micro.widgets.grid]</strong> extension widget."
        p
          innerValue:@"The first grid we display here, are simply using static items,
to create a non-interactive table, without any fancy stuff. The second grid is first of all dynamically populated, secondly it creates a couple
of 'template items', which instead of simply displaying static text, in fact holds a <strong>[widgets]</strong> collection by itself - 
In addition to adding some properties unto the actual row itself, by using the <strong>[.row]</strong> argument."


/*
 * Grid widget example.
 */
create-widget
  parent:content
  class:row air-top
  widgets
    div
      class:col-50
      widgets
        div
          class:bg air-inner shaded rounded
          widgets
            h4
              innerValue:Static datagrid
            p
              innerValue:This is a static datagrid, which simply renders as a static HTML table, without any fancy stuff.
            p
              innerValue:@"You can still invoke <strong>[micro.widgets.grid.databind]</strong> for it, to dynamically change its content later if you wish.
Which is useful to for instance being able to implement your own paging logic, filtering, etc."
            p
              innerValue:Its overhead to your page is close to nothing, and in fact it renders as a simple HTML table.
    div
      class:col-50
      widgets
        div
          class:bg air-inner shaded rounded
          widgets

            /*
             * Our actual grid.
             */
            micro.widgets.grid
              columns
                Name
                Email
                Phone no
              rows
                item
                  Name:Thomas Hansen
                  Email:thomas@gaiasoul.com
                  Phone no:12345678
                item
                  Name:John Doe
                  Email:john@doe.com
                  Phone no:99887766
                item
                  Name:Johhny Two-Times
                  Email:johhny@two-times.com
                  Phone no:67676767
                item
                  Name:Jane Doe
                  Email:jane@doe.com
                  Phone no:99118822
                item
                  Name:Peter Fisher
                  Email:peter@fisher.com
                  Phone no:91919191
create-widget
  parent:content
  class:row air-top
  widgets
    div
      class:col-70
      widgets
        div
          class:bg air-inner shaded rounded
          widgets

            /*
             * Our actual grid.
             * Notice, this one is dynamically populated in the [micro.widgets.grid.databind] invocation below.
             * Also notice we set the style property for one of our [columns], in addition to creating a template
             * [widgets] collection for another of our rows.
             */
            micro.widgets.grid:second-grid
              columns
                Name
                Email
                Phone no
                  widgets
                    a
                      href:#
                      innerValue:Phone no
                      onclick
                        set-widget-property:x:/../*/_event?value
                          innerValue:Filtering
                Delete
                  style:"color:rgba(255,0,0,.4);"
    div
      class:col-30
      widgets
        div
          class:bg air-inner shaded rounded
          widgets
            h4
              innerValue:Dynamically populated datagrid
            p
              innerValue:@"This is a dynamically populated datagrid, with one template column, containing a link button - In addition to 
adding arguments to the actual creation of one of the rows itself."
            p
              innerValue:@"It is created without any initial rows, but dynamically populated by invoking <strong>[micro.widgets.grid.databind]</strong>.
Hint, try clicking the links."

/*
 * Populating second grid from above.
 * Notice, first we add our template column to each [item] below.
 */
add:x:/+/*
  src
    Delete
      widgets
        a
          href:#
          role:button
          innerValue:Del
          onclick
            p5.web.widgets.get-parent:x:/../*/_event?value
            p5.web.widgets.get-parent:x:/-/*/*?value
            delete-widget:x:/-/*/*?value
micro.widgets.grid.databind:second-grid
  item
    Name:Thomas Hansen
      style:"color:rgb(128,128,255);"
    Email:thomas@gaiasoul.com
    Phone no:12345678
  item

    /*
     * A [.row] argument like the following, appends all given arguments to the actual "tr" widget,
     * allowing you to trap [onclick] on the row, set CSS class for the "tr" element, etc, etc, etc.
     * We just set a simple style property for the row as a whole.
     */
    .row
      style:"background-color:rgba(128,128,255,.2);"
    Name:John Doe
    Email:john@doe.com
    Phone no:99887766
  item
    Name:Johhny Two-Times
    Email:johhny@two-times.com
    Phone no:67676767
  item
    Name:Jane Doe
    Email:jane@doe.com
    Phone no:99118822
  item
    Name:Peter Fisher
    Email:peter@fisher.com
    Phone no:91919191



/*
 * Grid widget explanation (more).
 */
create-widget
  parent:content
  class:row air-top
  widgets
    div
      class:col-100
      widgets
        p
          innerValue:@"So basically, you have 100% perfect control over the exact rendering of your grid, by making it possible for you
to modify any aspect of its rendering."

