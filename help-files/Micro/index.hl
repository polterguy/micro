/*
 * This is our main "landing page" for our Micro help files.
 */
h2
  innerValue:Micro

img
  style:"margin-left:1rem; float:right;max-width: 20%;"
  src:"https://phosphorusfive.files.wordpress.com/2018/03/micro-icon-dox.png"

p
  innerValue:@"Micro is a general purpose CSS framework, and an Ajax Widget library for Phosphorus Five. These parts of the documentation
explains how you can use Micro as you create your own applications."

p
  innerValue:@"The CSS parts of Micro is inspired by Bootstrap CSS, but Micro is <em>significantly smaller in size</em>
compared to Bootstrap. It also contains a rich extension Ajax widget library, with Ajax widgets such as a tree view,
tab view, modal widgets, etc - In addition to lots of helper Active Events, solving all sorts of common
needs you may have while creating your own apps and modules. Micro is the default CSS framework used in
Phosphorus Five."


/*
 * All files at "root" of sub section.
 */
container
  element:ul
  oninit

    /*
     * Retrieving all other Hyperlambda files in the help folder.
     */
    list-files:@MICRO/help-files/Micro/
      filter:.hl|.md

    /*
     * Removing "index.hl".
     */
    set:x:@"/@list-files/*/"":regex:/index.hl$/"""

    /*
     * Looping through each Hyperlambda file (except of course "index.hl").
     */
    for-each:x:/@list-files/*

      /*
       * Figuring out filename, without extension, and removing numbering parts.
       */
      split:x:/@_dp/#?name
        =:/
        =:.
      replace:x:/@split/0/-2?name
        src:regex:/[0-9]+ - /

      /*
       * Creating a list item, containing a hyperlink button, which once clicked,
       * loads and displays the specified help file.
       */
      create-widget
        parent:x:/../*/_event?value
        element:li
        widgets
          a
            href:#
            innerValue:x:/@replace?value
            .file:x:/@_dp/#?name
            onclick

              /*
               * Retrieving file name, loading and displaying file, by invoking common
               * widget lambda event, which does the heavy lifting for us.
               */
              get-widget-property:x:/../*/_event?value
                .file
              desktop.help.display-file:x:/@get-widget-property/*/*?value
